class GameRunner {
    // init app game params
    field CursorLogic cursor;
    field Drawer drawer;
    field Level level;
    field Map map;

    field int size, padding;
    field int w, h;
    
    // active zone
    field int x0, y0;
    field int cell_count;

    constructor GameRunner new() {
        let size = 16;
        let padding = 2;

        let w = 512;
        let h = 256;

        let x0 = 20;
        let y0 = 5;
        let cell_count = 10;

        let map = Map.new(size, w, h);
        let drawer = Drawer.new(size, padding, w, h);
        let cursor = CursorLogic.new(x0, y0, cell_count);
        let level = Level.new(map, size, cell_count, x0, y0, w, h);
        return this;
    }

    // основной поток программы
    method void run () {
        var char key;
        var boolean exit;

        let exit = false;
        while (~exit) {    
            do Sys.wait(200);         
            // before event
            do drawer.eraseCursor(cursor);

            // events
            if (key = 49) {do level.change_level(0, map);} // 1 - key
            if (key = 50) {do level.change_level(1, map);} // 2 - key
            if (key = 51) {do level.change_level(2, map);} // 3 - key
            if (key = 52) {do level.change_level(3, map);} // 4 - key
            if (key = 53) {do level.change_level(4, map);} // 5 - key
            
            if (key = 81) {let exit = true;} // q key
            if (key = 32) {do map.change_cell(cursor.getX(), cursor.getY());} // space key TODO
            if (key = 131) {do cursor.moveUp();} // up arrow key
            if (key = 133) {do cursor.moveDown();} // down arrow key
            if (key = 130) {do cursor.moveLeft();} // left arrow key
            if (key = 132) {do cursor.moveRight();} // right arrow key

            if (level.check_done(map)) {
                do level.set_cur_status(2);
            }

            // after event
            do drawer.drawCursor(cursor);
            do drawer.drawMap(map);
            do drawer.drawGrid(x0, y0, size, cell_count);
            do drawer.drawMenu(level); 
            
            let key = 0;      

            // check events
            while (key = 0) {
                let key = Keyboard.keyPressed();
            }
        }
        return;
    }
}