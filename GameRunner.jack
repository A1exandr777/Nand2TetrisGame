class GameRunner {
    // init app game params
    field CursorLogic cursor;
    field Drawer drawer;
    field Level level;
    field Map map;

    field int size, padding;
    field int w, h;

    constructor GameRunner new() {
        let size = 16;
        let padding = 2;

        let w = 512;
        let h = 256;

        let map = Map.new(size, w, h);
        let drawer = Drawer.new(size, padding, w, h);
        let cursor = CursorLogic.new(size, w, h);
        
        return this;
    }
    // основной поток программы
    method void run () {
        var char key;
        var boolean exit;

        let exit = false;
        while (~exit) {
            // check events
            while (key = 0) {
                let key = Keyboard.keyPressed();
            }
            
            // before event
            do drawer.eraseCursor(cursor);

            // event
            if (key = 81) {let exit = true;} // q key
            if (key = 32) {do map.set_cell(cursor.getX(), cursor.getY());} // space key TODO
            if (key = 131) {do cursor.moveUp();} // up arrow key
            if (key = 133) {do cursor.moveDown();} // down arrow key
            if (key = 130) {do cursor.moveLeft();} // left arrow key
            if (key = 132) {do cursor.moveRight();} // right arrow key

            do Sys.wait(200);

            // after event
            do drawer.drawCursor(cursor);
            do drawer.drawGrid(160, 32, 16, 12);
            do drawer.drawMap(map);
            
            let key = 0;
        }
        return;
    }
}